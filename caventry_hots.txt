@@ Caventry Hots
@@ A chili dog cart.
@@ Requires the parent objects in "parent_drink.txt" and "parent_food.txt".

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/caventry_hots.txt

@@ Created and tested under PennMUSH v1.8.7

@@ Note: The "check_gold()" and "charge_gold()" functions are attributes defined
@@ in a bank object and exported using @function. See "bank.txt" and
@@ "gold_satchel.txt" for more info on the money system in use.

@create Caventry Hots
@lock/basic Caventry Hots==#1
@lock/enter Caventry Hots==#1
@lset Caventry Hots/basic=NO_INHERIT
@lset Caventry Hots/enter=NO_INHERIT
@set Caventry Hots=!NO_COMMAND
@set Caventry Hots=MONITOR
@set Caventry Hots=WIZARD

&FAILURE Caventry Hots=The cart belongs to the vendor!
&EFAIL Caventry Hots=The hotdog cart would be a tight fit. To see the menu, type [ansi(h,menu)].
&USE Caventry Hots=To see the menu, type [ansi(h,menu)].

&DESCRIBE Caventry Hots=A vendor with a food cart selling chili dogs. To see the menu, type [ansi(h,menu)].

&SHOW_MENU Caventry Hots=align(1 77,,table(iter(%0,##,|,|),20,77,|,|))
&DRINKS Caventry Hots=Dr. Rocket|Nuka Cola|Moon Mist
&FOOD Caventry Hots=Chili Dog|Chili Cheese Dog|Slaw Dog|The Works
&ITEM_PRICE Caventry Hots=1

&CMD_MENU Caventry Hots=$menu:@pemit %#=%r[ansi(h,Drinks)]:%r%r[u(show_menu,v(drinks))]%r%r[ansi(h,Food)]:%r%r[u(show_menu,v(food))]%r%rOne gold each. To order something, type [ansi(h,order <food or drink>)].

&CMD_TALK Caventry Hots=$talk *:@assert strmatch(name(%!),%0);@pemit %#=The vendor nods. To see the menu, type [ansi(h,menu)].

&FIND_ITEM Caventry Hots=grab(strcat(v(drinks),|,v(food)),%0,|)

&GET_ACTION Caventry Hots=if(match(v(drinks),%0,|),drink,eat)

&GET_ITEM_PARENT Caventry Hots=if(match(v(drinks),%0,|),get_parent_drink(),get_parent_food())

&BUSY Caventry Hots=0

&MAKE_ITEM Caventry Hots=[setq(0,[u(find_item,%0)])][setq(1,create(%q0))][setq(2,capstr(get_art(%q0)) %q0. To use\, type [ansi(h,use %q0)] or [ansi(h,[u(get_action,%q0)] %q0)].)][attrib_set(%q1/describe,%q2)][attrib_set(%q1/charges,2)][set(%q1,DESTROY_OK)][set(%q1,MONITOR)][set(%q1,QUIET)][set(%q1,!NO_COMMAND)][tel(%q1,me)]%q1

&CMD_ORDER Caventry Hots=$order *:@break v(busy)=@pemit %#=The vendor is serving another customer right now.;@assert t(u(find_item,%0))=@pemit %#=Never heard of it. To see the menu, type [ansi(h,menu)]. To order something, type [ansi(h,order <food or drink>)].;@assert check_gold(%#,v(item_price))=@pemit %#=You don't have enough gold!;@set %!=busy:1;think setq(0,u(make_item,%0));say You got it. That's one gold.;@wait 1={@parent %q0=[u(get_item_parent,name(%q0))];give %q0 to %#;@lock/use %q0=+%q0;@oemit %#=The vendor gives %n [get_art(name(%q0))] [name(%q0)].};think charge_gold(%#,v(item_price));@set %!=busy:0