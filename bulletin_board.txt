@@ Bulletin Board
@@ A community bulletin board to post and read messages.

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/bulletin_board.txt

@@ Created and tested under PennMUSH v1.8.7

@create Bulletin Board
@set Bulletin Board=QUIET MONITOR !NO_COMMAND WIZARD
@lock/basic Bulletin Board==#1
@lock/enter Bulletin Board==#1

&DESCRIBE Bulletin Board=A large cube with a corkboard surface that residents and travelers can use to post notes.%rThere are notes from the following people:%r%r[u(print_authors)]%r%rTo read a note, type [ansi(h,read <author>)]. To leave a note, type [ansi(h,write <message>)]. To remove your note, type [ansi(h,remove)].

&USE Bulletin Board=There are notes from the following people:%r%r[u(print_authors)]%r%rTo read a note, type [ansi(h,read <author>)]. To leave a note, type [ansi(h,write <message>)]. To remove your note, type [ansi(h,remove)].

&CMD_WRITE Bulletin Board=$write *:think [attrib_set(%!/NOTE_[name(%#)],[edit(decompose(left(%0,500)),\\\,,\,)])];think [attrib_set(%!/AUTHORS,[setunion(v(authors),name(%#))])];@pemit %#=Your note has been pinned to the board.;@oemit %#=[name(%n)] writes on the bulletin board:%r%r[align(1 77,,v(note_[name(%#)])%r%r)]

&CMD_READ Bulletin Board=$read *:@if t(v(note_%0))={@pemit %#=[capstr(%0)] writes:%r%r[align(1 77,,v(note_%0)%r%r)]},{@pemit %#=There is no note by that author.}

&CMD_REMOVE Bulletin Board=$remove:think attrib_set(%!/AUTHORS,[remove(v(authors),name(%#))]);think attrib_set(%!/NOTE_[name(%#)]);@pemit %#=Removed.

&PRINT_AUTHORS Bulletin Board=align(1 77,,Mayor Carmel%r[iter(v(authors),%i0,,%r)])

&NOTE_MAYOR_CARMEL Bulletin Board=Welcome to Caventry! Our seaside village has a noble history and a little magic for you to discover. Check back here for the latest news!

&TOWN_UPDATES Bulletin Board=1/30/2020: Some spring flowers have appeared early near the shore.

&CMD_READ Bulletin Board=$read *:@select [lcstr(%0)]=mayor carmel, {@pemit %#=Mayor Carmel writes:%r%r[align(1 77,,v(note_mayor_carmel)%r%r[iter(v(town_updates),%i0,|,%r%r)]%r%r)]}, {@if t(v(note_%0))={@pemit %#=[capstr(%0)] writes:%r%r[align(1 77,,v(note_%0)%r%r)]},{@pemit %#=There is no note by that author.}}