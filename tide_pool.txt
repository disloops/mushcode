@@ Tide Pool
@@ A little pool that forms in weathered stone.
@@ Uses the scorekeeping object in "scoreboard.txt".
@@ Requires the parent object in "parent_stone.txt".

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/tide_pool.txt

@@ Created and tested under PennMUSH v1.8.7

@create Tide Pool
@set Tide Pool=QUIET
@lock/basic Tide Pool==#1
@lock/enter Tide Pool==#1
@set Tide Pool=!NO_COMMAND
@set Tide Pool=MONITOR
@set Tide Pool=WIZARD

&DESCRIBE Tide Pool=There are some shallow pools among the rocks that have been filled with tide water. The base of these pools is a rust and turquoise-colored cross-section of the rock below. Some tiny minnows dart back and forth in the water and a bit of vegetation clings to the sides.
&USE Tide Pool=You don't want to disrupt the fragile ecosystem.
&OUSE Tide Pool=examines the tide pool.
&FAILURE Tide Pool=You can't take that.
&EFAIL Tide Pool=It's not really meant for swimming.

&BUSY Tide Pool=0

&MAKE_STONE Tide Pool=[setq(0,Blue Aragonite)][setq(1,create(%q0))][setq(2,The %q0 is a weathered gemstone in the shape of a small slab. It was found in the tide pools west of Caventry.)][attrib_set(%q1/describe,%q2)][set(%q1,QUIET)][tel(%q1,me)]%q1

&SEARCH Tide Pool=@break get_achievement(%0,3)=@pemit %0=Nothing was found.;@break v(busy)=@pemit %0=Someone else is standing too near the tide pool.;@set %!=busy:1;think setq(0,u(make_stone));think set_achievement(%0,3);@pemit %0=You examine the tide pool and see it teeming with life. Some minnows wait for a rogue wave to carry them back out to sea. Without disturbing any of the sea urchins, you are able to extract a smooth stone from the pool.;@wait 1={@parent %q0=[get_parent_stone()];@tel %q0=%0;@lock/use %q0=+%q0;@lock/enter %q0==#1;@emit [name(%0)] finds [get_art(name(%q0))] [name(%q0)].;@set %!=busy:0}