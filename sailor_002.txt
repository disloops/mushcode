@@ Sailor
@@ A sailor with a boat for rent.
@@ Requires the parent object in "parent_person.txt".

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/sailor_002.txt

@@ Created and tested under PennMUSH v1.8.7

@create Captain Drake
@lock/basic Captain Drake==#1
@lock/enter Captain Drake==#1
@lset Captain Drake/basic=NO_INHERIT
@lset Captain Drake/enter=NO_INHERIT
@set Captain Drake=!NO_COMMAND
@set Captain Drake=MONITOR
@set Captain Drake=WIZARD
@parent Captain Drake=get_parent_person()

&FAILURE Captain Drake=He can't come with you!
&EFAIL Captain Drake=He's not interested.
&USE Captain Drake=That won't get you far. Try [ansi(h,talk [name(%!)])] instead.

&DESCRIBE Captain Drake=A tall man who is dressed very leisurely for a captain. Try [ansi(h,talk [name(%!)])] to inquire about his ship.

&BUSY Captain Drake=0

&BOAT_PRICE Captain Drake=300

&BOAT_RENTED Captain Drake=t(namegrab(lcon(%#),Captain Drake's Boat Keys))

&HAS_COMPASS Captain Drake=t(namegrab(lcon(%#),Ship's Compass))

&TALK Captain Drake=@if t(namegrab(lcon(%0),Captain Drake's Boat Keys))={@pemit %0=[name(%!)] says, "Fair winds!"},{@pemit %0=[name(%!)] says, "There's a boat for rent if you're interested. It can hold enough petrol for three days at sea. Cost ya' [v(boat_price)] gold and you'll need a compass."%rTo rent a boat, type [ansi(h,rent boat)].}

&CMD_RENT Captain Drake=$rent boat:@break v(busy)=@pemit %#=Captain Drake is talking to someone else at the moment.;@break u(boat_rented)=@pemit %#=[name(%!)] says, "You've already got the keys.";@assert check_gold(%#,v(boat_price))=@pemit %#=[name(%!)] says, "You don't have enough gold!";@assert u(has_compass)=@pemit %#=[name(%!)] says, "You'll need a real compass before you can pilot the boat.";@set %!=busy:1;@pemit %#=[name(%!)] says, "That's a magnificent compass! Take the red trawler out in the harbor. Fair winds!";@wait 1={think setq(0,namegrab(lcon(%!),Captain Drake's Boat Keys));think setq(1,clone(%q0));@tel %q1=%!;@wait 1={give %q1 to %#;@lock/give %q1==#1;@lock/use %q1=+%q1;@oemit %#=[name(%!)] gives %n a set of boat keys.;think charge_gold(%#,v(boat_price));@set %!=busy:0}}