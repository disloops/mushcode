@@ The Crazy 5's Club
@@ A members only club.

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/offsite/8bit/tacosal/5555_club/club.txt

@NAME here=The Crazy 5's Club

@SET here=HAVEN
@SET here=JUMP_OK
@set here=OPAQUE

@LOCK/SPEECH here=FUNC_IS_MEMBER/1
@LOCK/TELEPORT here=FUNC_IS_MEMBER/1

@DESC here=if(match(get([Log Book DBREF]/MEMBERS),[name([owner(%#)])]\([owner(%#)]\),|),[u(SHOW_DESC)],[remit(%!,<5555> Spy! [ansi(yh,[name(%#)]\(%#\))] is looking at [name(%!)]\(%!\).)][if(not(strmatch([name(%#)]\(%#\),last(get([Log Book DBREF]/SPIES),|))),[if(get([Log Book DBREF]/SPIES),[set([Log Book DBREF],SPIES:[get([Log Book DBREF]/SPIES)]|[name(%#)]\(%#\))],[set([Log Book DBREF],SPIES:[name(%#)]\(%#\))])])]%r%bThis attempt has been logged.%r)

&FUNC_TRACKER here=@DOLIST unique(lwho())={@IF [hasattr(%!,DBREF_LOC_[unique(rest(##,#))])]={@IF [not(strmatch(loc(##),get(%!/DBREF_LOC_[unique(rest(##,#))])))]={th [remit(%!,<5555> [ansi(yh,[name(##)](##))] has moved from [name(get(%!/DBREF_LOC_[unique(rest(##,#))]))]([get(%!/DBREF_LOC_[unique(rest(##,#))])]) to [name(loc(##))]([loc(##)]).)];th [set(%!,DBREF_LOC_[unique(rest(##,#))]:[loc(##)])]}};@IF [not(hasattr(%!,DBREF_LOC_[unique(rest(##,#))]))]={th [remit(%!,<5555> [ansi(yh,[name(##)](##))] is online! They are located in [name(loc(##))]([loc(##)]).)];th [set(%!,DBREF_LOC_[unique(rest(##,#))]:[loc(##)])]}};@WAIT 5=@TRIGGER me/FUNC_TRACKER

@AHOURLY here=@IF [not(words(qentries(me)))]={@HALT me=@TRIGGER me/FUNC_TRACKER;@remit #5555=%r[ansi(yh,<5555>)] Channel restarted by @ahourly.}

@STARTUP here=@HALT me=@TRIGGER me/FUNC_TRACKER

&SHOW_DESC here=%r%b%b%b%b%b_________________%r%b%b%b%b/%b%b%b%b%b%b%b%b%b%b%b%b%b%b%b%b/|%r%b%b%b/_______________%b/%b|%r%b%b|%b%b%b%b%b%b%b%b%b%b%b%b%b%b%b%b|%b%b|%r%b%b|%b%b%b%b%b%b#5555%b%b%b%b%b|%b%b|%r%b%b|%b%b%b%b%b%b%b%b%b%b%b%b%b%b%b%b|%b%b.%r%b%b|%b%b%b\[\]%b%b%b_%b%b%b\[\]%b%b|%b/%r%b%b|_______|||______|/%r%r[ansi(g,Members:)]%r[u(FUNC_SHOW_MEMBERS)]%r%r[ansi(r,Recent Spies:)]%r[u(FUNC_SHOW_SPIES)]%r%r[ansi(m,Recent Visitors:)]%r[u(FUNC_SHOW_VISITORS)]%r%r[ansi(y,Commands:)]%r[ansi(w,members)] - List members%r[ansi(w,spies)] - View spies%r[ansi(w,history)] - Show club history%r[ansi(w,scry)] - See random MUSH code%r[ansi(w,jump)] - Teleport to random room%r

&CMD_MEMBERS here=$members:@PEMIT %#=%r[ansi(yh,The Crazy 5's Club Members:)]%r%r[u(FUNC_SHOW_MEMBERS)]

&FUNC_SHOW_MEMBERS here=[iter(get([Log Book DBREF]/MEMBERS),##,|,%r)]

&CMD_SPIES here=$spies:@PEMIT %#=%r[ansi(yh,Who has attended to breach the Club?)]%r%r[u(FUNC_SHOW_SPIES)]

&FUNC_SHOW_SPIES here=[iter(get([Log Book DBREF]/SPIES),##,|,%r)]

&FUNC_SHOW_VISITORS here=[if(get([Log Book DBREF]/VISITORS),[iter(extract(revwords(get([Log Book DBREF]/VISITORS),|),1,5,|),##,|,%r)],None!)]

&FUNC_IS_MEMBER here=or(t(grab(get([Log Book DBREF]/MEMBERS),[name(%#)](%#),|)),strmatch(%#,#5555))

&CMD_HISTORY here=$history:@PEMIT %#={%r[ansi(yh,Club History)]%r%rAt some point I became interested in creating a secret club and criminal organization within 8Bit. I thought the headquarters should have a convenient DBREF and I began looking at existing objects. One caught my eye:%r%rBird God Statue (#5555)%rThe stone carving is quite intricate and the eyes are of ruby gemstones. The statue seems to have a magical aura about it.%rOwner: 8BitMUSH Zone: *NOTHING* Coins: 1%rBasic Lock: Connecting..(#0)%rHome: Pre-8Bit: Dynamix Era Artifacts%rLocation: Pre-8Bit: Dynamix Era Artifacts%r%rThe Bird God's DBREF would make a good home for the new club. Very carefully, I teleported it to my location and @CLONED a copy of it. It was also necessary to @LINK it to the "Pre-8Bit: Dynamix Era Artifacts" (#11440) exhibit and @LOCK it to the "Connecting.." room, which is DBREF (#0).%r%rI teleported the phony statue back to the museum, where it sits today. I still had no ability to change the type of an existing object to a room, so I @NUKED the original statue and quickly dug a new room, hoping that (#5555) would be the first available DBREF. This is how The Crazy 5's Club (#5555) was created.}

@AENTER here=@IF or(and(strmatch(THING,[type(%#)]),not(match([get([Log Book DBREF]/MEMBERS)],[name([owner(%#)])]\([owner(%#)]\),|))),and(strmatch(PLAYER,[type(%#)]),not(match([get([Log Book DBREF]/MEMBERS)],[name(%#)]\(%#\),|))))={@REMIT %!={<5555> Spy! [ansi(yh,[name(%#)]\(%#\))] is in [name(%!)]\(%!\).};@IF and(not(strmatch([name(%#)],Meeple)),not(strmatch([name(%#)]\(%#\),last(get([Log Book DBREF]/SPIES),|))))={@IF [get([Log Book DBREF]/SPIES)]={@SET [Log Book DBREF]={SPIES:[get([Log Book DBREF]/SPIES)]|[name(%#)]\(%#\)}},{@SET [Log Book DBREF]={SPIES:[name(%#)]\(%#\)}}};@TEL %#=#10},{@IF and(strmatch(PLAYER,[type(%#)]),not(strmatch([name(%#)]\(%#\),TacoSal\([my DBREF]\))),not(strmatch([name(%#)]\(%#\),last(get([Log Book DBREF]/VISITORS),|))))={@IF [get([Log Book DBREF]/VISITORS)]={@SET [Log Book DBREF]={VISITORS:[get([Log Book DBREF]/VISITORS)]|[name(%#)]\(%#\)}},{@SET [Log Book DBREF]={VISITORS:[name(%#)]\(%#\)}}}}

&CMD_SCRY here=$scry:think setq(0,pick(lattr([Visuals DBREF]/VISUAL_*)));think setq(1,rest(%q0,_));think setq(2,#%q1);@PEMIT %#=%r[ansi(yh,Here's a random object:)]%r%r[ansi(wh,NAME)]: [name(%q2)]%b\(%q2\)%r[ansi(wh,TYPE)]: [type(%q2)]%r[ansi(wh,OWNER)]: [name([owner(%q2)])]\([owner(%q2)]\)%r[if(and(not(strmatch([parent(%q2)],#-1*)),[parent(%q2)]),[ansi(wh,PARENT)]: [parent(%q2)]%r)][if(and(not(strmatch([flags(%q2)],#-1*)),flags(%q2)),[ansi(wh,FLAGS)]: [flags(%q2)]%r)][if(and(not(strmatch([powers(%q2)],#-1*)),powers(%q2)),[ansi(wh,POWERS)]: [powers(%q2)]%r)][if(strmatch([lattr(%q2)],#-1*),[ansi(wh,ATTRIBUTES)]: Cannot get attributes%r,[iter(lattr(%q2),[ansi(wh,%i0)]: [get(%q2/%i0)], ,%r)][if(lattr(%q2),%r)])][if(strmatch([lcon(%q2)],#-1*),[ansi(wh,INVENTORY)]: Cannot get inventory%r,[if(lcon(%q2),[ansi(wh,INVENTORY)]:%r[iter(lcon(%q2),[name(%i0)]\(%i0\), ,%r)][if(lcon(%q2),%r)])])][if(and(not(strmatch(ROOM,[type(%q2)])),not(strmatch([loc(%q2)],#-1*))),[ansi(wh,LOCATION)]: [name([loc(%q2)])]\([loc(%q2)]\)%r)][if(and(not(strmatch(ROOM,[type(%q2)])),not(strmatch([home(%q2)],#-1*))),[ansi(wh,HOME)]: [name([home(%q2)])]\([home(%q2)]\)%r)]

&CMD_JUMP here=$jump:think setq(0,pick(lattr([Jumps DBREF]/JUMP_*)));think setq(1,rest(%q0,_));think setq(2,#%q1);@PEMIT %#={%r[ansi(yh,Teleporting you to [name(%q2)]\(%q2\) in 3...2...1...)]%r};@WAIT 3=@TEL %#=%q2
