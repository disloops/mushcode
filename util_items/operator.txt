@@ The Operator
@@ A reference object that exports global commands.
@@ This object should be in the Master Room.
@@ Requires the parent objects in "parent_drink.txt" and "parent_food.txt".

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/util_items/operator.txt

@tel me=#2
@create Operator
@set Operator=QUIET
@set Operator=!NO_COMMAND
@set Operator=WIZARD
@set Operator=SAFE
@lock/basic Operator==#1
@lock/enter Operator==#1

&DESCRIBE Operator=Here for technical assistance.

&CMD_DRINK Operator=$drink *:@if strmatch(%0,#*)={@assert strmatch(loc(%0),%#)=@pemit %#=You don't have that.;@assert strmatch(lparent(%0),*[get_parent_drink()]*)=@pemit %#=You can't drink that!;@verb %0=%#,get_use_text,You drink the [capstr(name(%0))],get_ouse_text,drinks [get_art(name(%0))] [capstr(name(%0))].;@if gt(get(%0/charges),0)={@charges %0=dec(get(%0/charges));},{@pemit %#=You finish your drink.;@set %0=DARK;@nuke %0;@nuke %0;}},{@break gte(words(namegraball(lcon(%#),%0)),2)=@pemit %#=I don't know which one you mean!;@assert setr(0,locate(%#,%0,TXi))=@pemit %#=You don't have that.;@assert strmatch(lparent(%q0),*[get_parent_drink()]*)=@pemit %#=You can't drink that!;@verb %q0=%#,get_use_text,You drink the [capstr(name(%q0))],get_ouse_text,drinks [get_art(name(%q0))] [capstr(name(%q0))].;@if gt(get(%q0/charges),0)={@charges %q0=dec(get(%q0/charges));},{@pemit %#=You finish your drink.;@set %q0=DARK;@nuke %q0;@nuke %q0;}}

&CMD_EAT Operator=$eat *:@if strmatch(%0,#*)={@assert strmatch(loc(%0),%#)=@pemit %#=You don't have that.;@break strmatch(lparent(%0),*[get_parent_fish()]*)=@pemit %#=It seems a little to lively to be sushi.;@assert strmatch(lparent(%0),*[get_parent_food()]*)=@pemit %#=You can't eat that!;@verb %0=%#,get_use_text,You eat the [capstr(name(%0))],get_ouse_text,eats [get_art(name(%0))] [capstr(name(%0))].;@if gt(get(%0/charges),0)={@charges %0=dec(get(%0/charges));},{@pemit %#=You finish your food.;@set %0=DARK;@nuke %0;@nuke %0;}},{@break gte(words(namegraball(lcon(%#),%0)),2)=@pemit %#=I don't know which one you mean!;@assert setr(0,locate(%#,%0,TXi))=@pemit %#=You don't have that.;@assert strmatch(lparent(%q0),*[get_parent_food()]*)=@pemit %#=You can't eat that!;@verb %q0=%#,get_use_text,You eat the [capstr(name(%q0))],get_ouse_text,eats [get_art(name(%q0))] [capstr(name(%q0))].;@if gt(get(%q0/charges),0)={@charges %q0=dec(get(%q0/charges));},{@pemit %#=You finish your food.;@set %q0=DARK;@nuke %q0;@nuke %q0;}}

&CMD_STEAL Operator=$steal *:@pemit %#=You had better not!

&CMD_QUIT Operator=$quit:@pemit %#=Type 'QUIT' in all caps to exit.
&CMD_EXIT Operator=$exit:@pemit %#=Type 'QUIT' in all caps to exit.

&CMD_TALK Operator=$^talk\ (to\ )?(.+)$:@if strmatch(%2,#*)={@select words(matchall(lthings(loc(%#)),%2),)=0,{@break locate(%#,%2,)=@pemit %#=There is no reply.;@pemit %#=I don't see them here.},1,{@if hasattrval(%2/talk)={@trigger/inplace %2/talk=%#},{@pemit %#=There is no reply.}},{@pemit %#=I don't know which one you mean!}},{@select setr(0,locate(%#,%2,))=#-1,{@pemit %#=They cannot be found.},#-2,{@pemit %#=I don't know which one you mean!},{@if hasattrval(%q0/talk)={@trigger/inplace %q0/talk=%#},{@pemit %#=There is no reply.}}}

@set Operator/CMD_TALK=regexp

&CMD_PICK Operator=$pick *:@if strmatch(%0,#*)={@select words(matchall(lthings(loc(%#)),%0),)=0,{@break locate(%#,%0,)=@pemit %#=How would that work?;@pemit %#=It cannot be found.},1,{@if strmatch(lparent(%0),*[get_parent_flowers()]*)={@pemit %#=Try to [ansi(h,take)] it instead.},{@pemit %#=How would that work?}},{@pemit %#=It cannot be found.}},{@select setr(0,locate(%#,%0,))=#-1,{@pemit %#=It cannot be found.},#-2,{@pemit %#=I don't know which one you mean!},{@if strmatch(lparent(%q0),*[get_parent_flowers()]*)={@pemit %#=Try to [ansi(h,take)] it instead.},{@pemit %#=How would that work?}}}

&CMD_SEARCH Operator=$^search\ (the\ )?(.+)$:@if strmatch(%2,#*)={@select words(matchall(lthings(loc(%#)),%2),)=0,{@break strmatch(loc(%#),%2)={@if hasattrval(%2/search)={@trigger/inplace %q0/search=%#},{@pemit %#=Nothing was found.}};@break locate(%#,%2,)=Nothing was found.;@pemit %#=That cannot be found.},1,{@if hasattrval(%2/search)={@trigger/inplace %2/search=%#},{@pemit %#=Nothing was found.}},{@pemit %#=I don't know which one you mean!}},{@select setr(0,locate(%#,%2,))=#-1,{@pemit %#=That cannot be found.},#-2,{@pemit %#=I don't know which one you mean!},{@if hasattrval(%q0/search)={@trigger/inplace %q0/search=%#},{@pemit %#=Nothing was found.}}}

@set Operator/CMD_SEARCH=regexp

&CMD_+TODAY Operator=$+today:@pemit %#=Generating +today...;th setq(Content-Type,application/json);th setq(0,json(object, auth, json(string, [get_mushgpt_pw()]), char, json(string, today)));@http/post %!/print_today=[get_mushgpt_url()]/cmd, %q0

&CMD_HELPER Operator=[edit(%0,\\",",%%r,%r)]

&PRINT_TODAY Operator=th setq(1,[trim(json_query(%0, get, message),")]);@break strmatch(%q1,Failed:*)={@pemit %#=I can't retrieve that data. It must be the END OF DAYS!};@pemit %#=%r[u(CMD_HELPER,%q1)]%r
