@@ The Operator
@@ A reference object that exports global commands.
@@ This object should be in the Master Room.
@@ Requires the parent objects in "parent_drink.txt" and "parent_food.txt".

@@ author = Matt Westfall
@@ email = disloops@gmail.com
@@ source = https://github.com/disloops/mushcode/blob/master/util_items/operator.txt

@tel me=#2
@create Operator
@set Operator=QUIET
@set Operator=MONITOR
@set Operator=!NO_COMMAND
@set Operator=WIZARD
@set Operator=SAFE
@lock/basic Operator==#1
@lock/enter Operator==#1

&DESCRIBE Operator=Here for technical assistance.

&CMD_DRINK Operator=$drink *:@if strmatch(%0,#*)={@assert match(loc(%0),%#)=@pemit %#=You don't have that.;@assert match(lparent(%0),get_parent_drink())=@pemit %#=You can't drink that!;@verb %0=%#,get_use_text,You drink the [capstr(name(%0))],get_ouse_text,drinks [get_art(name(%0))] [capstr(name(%0))].;@if gt(get(%0/charges),0)={@charges %0=dec(get(%0/charges));},{@pemit %#=You finish your drink.;@set %0=DARK;@nuke %0;@nuke %0;}},{@break gte(words(namegraball(lcon(%#),%0)),2)=@pemit %#=I don't know which one you mean!;@assert setr(0,locate(%#,%0,TXi))=@pemit %#=You don't have that.;@assert match(lparent(%q0),get_parent_drink())=@pemit %#=You can't drink that!;@verb %q0=%#,get_use_text,You drink the [capstr(name(%q0))],get_ouse_text,drinks [get_art(name(%q0))] [capstr(name(%q0))].;@if gt(get(%q0/charges),0)={@charges %q0=dec(get(%q0/charges));},{@pemit %#=You finish your drink.;@set %q0=DARK;@nuke %q0;@nuke %q0;}}

&CMD_EAT Operator=$eat *:@if strmatch(%0,#*)={@assert match(loc(%0),%#)=@pemit %#=You don't have that.;@break match(lparent(%0),get_parent_fish())=@pemit %#=It seems a little to lively to be sushi.;@assert match(lparent(%0),get_parent_eat())=@pemit %#=You can't eat that!;@verb %0=%#,get_use_text,You eat the [capstr(name(%0))],get_ouse_text,eats [get_art(name(%0))] [capstr(name(%0))].;@if gt(get(%0/charges),0)={@charges %0=dec(get(%0/charges));},{@pemit %#=You finish your food.;@set %0=DARK;@nuke %0;@nuke %0;}},{@break gte(words(namegraball(lcon(%#),%0)),2)=@pemit %#=I don't know which one you mean!;@assert setr(0,locate(%#,%0,TXi))=@pemit %#=You don't have that.;@assert match(lparent(%q0),get_parent_food())=@pemit %#=You can't eat that!;@verb %q0=%#,get_use_text,You eat the [capstr(name(%q0))],get_ouse_text,eats [get_art(name(%q0))] [capstr(name(%q0))].;@if gt(get(%q0/charges),0)={@charges %q0=dec(get(%q0/charges));},{@pemit %#=You finish your food.;@set %q0=DARK;@nuke %q0;@nuke %q0;}}

&CMD_STEAL Operator=$steal *:@pemit %#=You had better not!
&CMD_SEARCH Operator=$search*:@pemit %#=Nothing was found.

&CMD_QUIT Operator=$quit:@pemit %#=Type 'QUIT' in all caps to exit.
&CMD_EXIT Operator=$exit:@pemit %#=Type 'QUIT' in all caps to exit.

&CMD_TALK Operator=$talk *:@if strmatch(%0,#*)={@select items(matchall(lthings(loc(%#)),%0),)=0,{@pemit %#=They cannot be found.},1,{@if hasattrval(%0/talk)={@trigger/inplace %q0/talk=%#},{@pemit %#=There is no reply.}},{@pemit %#=I don't know which one you mean!}},{@select setr(0,locate(%#,%0,TFn))=#-1,{@pemit %#=They cannot be found.},#-2,{@pemit %#=I don't know which one you mean!},{@if hasattrval(%q0/talk)={@trigger/inplace %q0/talk=%#},{@pemit %#=There is no reply.}}}